# Copyright (c) 2022-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("//foundation/ability/ability_runtime/ability_runtime.gni")
import("//foundation/arkui/ace_engine/test/unittest/ace_unittest.gni")

config("config_svg_parse_test") {
  visibility = [ ":*" ]
  include_dirs = [
    "//commonlibrary/c_utils/base/include",
    "//foundation/graphic/graphic_2d/rosen/modules/2d_engine",
    "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/include",
    "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/src",
    "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/src/drawing/engine_adapter",
    "//third_party/skia",
    "$ace_root",
    "$ace_root/frameworks",
  ]
  if (enable_graphic_text_gine) {
    include_dirs += [ "//foundation/graphic/graphic_2d/rosen/modules/2d_engine/rosen_text/export" ]
  } else {
    include_dirs +=
        [ "//foundation/graphic/graphic_2d/rosen/modules/2d_engine/rosen_text" ]
  }
}

ohos_unittest("parse_test_ng") {
  module_out_path = svg_test_output_path

  sources = [ "parse_test_ng.cpp" ]

  deps = [
    "$ace_root/build:ace_ohos_unittest_base",
    "$ace_root/frameworks/core/components_ng/base:ace_core_components_base_ng_ohos",
    "//third_party/googletest:gtest_main",
  ]

  configs = [
    ":config_svg_parse_test",
    "$ace_root:ace_config",
  ]

  if (ability_runtime_graphics) {
    external_deps = [
      "ability_runtime:runtime",
      "image_framework:image_native",
      "window_manager:libdm",
    ]
  }
}

ohos_unittest("svg_dom_test_ng") {
  module_out_path = svg_test_output_path

  sources = [
    "$ace_root/frameworks/core/common/container_scope.cpp",
    "$ace_root/frameworks/core/components_ng/render/adapter/image_painter_utils.cpp",
    "$ace_root/frameworks/core/components_ng/svg/svg_context.cpp",
    "$ace_root/frameworks/core/components_ng/svg/svg_dom.cpp",
    "$ace_root/test/mock/core/animation/mock_animator.cpp",
    "svg_dom_test_ng.cpp",
  ]

  deps = [
    "$ace_root/frameworks/base:ace_memory_monitor_ohos",
    "$ace_root/test/unittest:ace_unittest_log",
    "$ace_root/test/unittest:ace_unittest_trace",
    "$cjson_root:cjson_static",
    "//third_party/googletest:gmock_main",
  ]

  configs = [
    "$ace_root/test/unittest:ace_unittest_config",
    ":config_svg_parse_test",
  ]
}

group("core_svg_unittest") {
  testonly = true
  deps = [
    ":parse_test_ng",
    ":svg_dom_test_ng",
  ]
}
