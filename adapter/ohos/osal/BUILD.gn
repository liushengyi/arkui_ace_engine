# Copyright (c) 2021-2023 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/ability/ability_runtime/ability_runtime.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import("//foundation/arkui/ace_engine/build/ace_ext.gni")

# build static
template("ace_osal_ohos_source_set") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
    defines += invoker.defines
    if (defined(vendor_configs.use_ext_surface) &&
        vendor_configs.use_ext_surface) {
      defines += [ "EXT_SURFACE_ENABLE" ]
    }
    if (is_ohos_standard_system) {
      external_deps = [
        "hilog:libhilog",
        "hitrace:hitrace_meter",
        "init:libbeget_proxy",
        "init:libbegetutil",
      ]
      configs = [ "$ace_root:ace_config" ]
    } else {
      external_deps = [
        "hilog:libhilog",
        "hitrace:hitrace_meter",
        "init:libbegetutil",
      ]

      configs = [ "$ace_root:ace_config" ]
    }
    include_dirs = [
      "$ability_runtime_napi_path/inner/napi_common",
      "//base/hiviewdfx/hicollie/interfaces/native/innerkits/include",
      "//commonlibrary/c_utils/base/include",
      "//foundation/window/window_manager/interfaces/innerkits",
    ]

    sources = [
      "ace_checker.cpp",
      "ace_engine_ext.cpp",
      "ace_trace.cpp",
      "anr_thread.cpp",
      "app_bar_helper_impl.cpp",
      "data_detector_loader.cpp",
      "data_detector_mgr.cpp",
      "event_report.cpp",
      "exception_handler.cpp",
      "frame_report.cpp",
      "layout_inspector.cpp",
      "log_wrapper.cpp",
      "modal_ui_extension_impl.cpp",
      "ressched_report.cpp",
      "socperf_client_impl.cpp",
      "system_properties.cpp",
      "trace_id_impl.cpp",
      "view_data_wrap_ohos.cpp",
      "want_wrap_ohos.cpp",
    ]

    external_deps += [
      "ability_runtime:app_manager",
      "init:libbegetutil",
    ]

    if (is_ohos_standard_system) {
      sources += [
        "resource_adapter_impl.cpp",
        "resource_adapter_impl_v2.cpp",
        "resource_convertor.cpp",
        "resource_theme_style.cpp",
      ]
      deps = [
        "$ace_flutter_engine_root/icu:ace_libicu_ohos",
        "$skia_root_new:skia_ohos",
      ]

      if (defined(global_parts_info) &&
          defined(global_parts_info.resourceschedule_soc_perf)) {
        external_deps += [ "soc_perf:socperf_client" ]
        defines += [ "SOC_PERF_ENABLE" ]
      }

      external_deps += [
        "ability_base:view_data",
        "ability_base:want",
        "ability_runtime:abilitykit_native",
        "image_framework:image",
        "input:libmmi-client",
        "napi:ace_napi",
        "netmanager_base:net_conn_manager_if",
        "resource_management:global_resmgr",
      ]
      if (defined(config.accessibility_support) &&
          config.accessibility_support) {
        sources += [ "js_accessibility_manager.cpp" ]
        deps += [ "$ace_root/frameworks/bridge/common/accessibility:bridge_accessibility_$platform" ]
        external_deps += [
          "accessibility:accessibilityclient",
          "accessibility:accessibilityconfig",
        ]
      } else {
        sources += [ "fake_accessibility_manager.cpp" ]
      }
      if (frame_trace_support) {
        include_dirs += [ "//foundation/resourceschedule/frame_aware_sched/interfaces/innerkits/frameintf" ]
        sources += [ "frame_trace_adapter_impl.cpp" ]
        external_deps += [ "frame_aware_sched:frame_trace_intf" ]
      } else {
        sources += [ "frame_trace_adapter_fake_impl.cpp" ]
      }
    } else {
      external_deps += [ "multimedia_image:image" ]
    }
    sources += [
      "download_manager_ohos.cpp",
      "download_manager_ohos_v2.cpp",
      "file_uri_helper_ohos.cpp",
      "image_source_ohos.cpp",
      "mouse_style_ohos.cpp",
      "page_url_checker_ohos.cpp",
      "pixel_map_ohos.cpp",
    ]
    external_deps += [
      "ability_runtime:abilitykit_native",
      "ability_runtime:app_manager",
      "app_file_service:fileuri_native",
      "bundle_framework:appexecfwk_core",
      "graphic_2d:librender_service_client",
      "hichecker:libhichecker",
      "hicollie:libhicollie",
      "hisysevent:libhisysevent",
      "hitrace:libhitracechain",
      "netstack:http_client",
      "safwk:system_ability_fwk",
      "samgr:samgr_proxy",
      "window_manager:scene_session",
    ]
    deps += [
      "$ace_root/interfaces/inner_api/drawable_descriptor:drawable_descriptor",
    ]

    if (ability_runtime_graphics) {
      external_deps += [
        "image_framework:image_native",
        "window_manager:libdm",
      ]
    }
  }
}

foreach(item, ace_platforms) {
  platform = item.name
  if (platform == "ohos" || platform == "ohos_ng") {
    ace_osal_ohos_source_set("ace_osal_ohos_${platform}") {
      platform = item.name
      assert(defined(platform) && platform != "", "platform must be defined")
      defines = []
      config = {
      }

      if (defined(item.config)) {
        config = item.config
      }
      if (defined(config.defines)) {
        defines = config.defines
      }
    }
  }
}
