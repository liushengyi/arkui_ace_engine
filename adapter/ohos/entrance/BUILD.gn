# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

# build static
template("ace_ohos_standard_source_set") {
  forward_variables_from(invoker, "*")

  ohos_source_set(target_name) {
    subsystem_name = ace_engine_subsystem
    part_name = ace_engine_part
    defines += invoker.defines
    cflags_cc += invoker.cflags_cc

    sources = [
      "$ace_root/adapter/ohos/entrance/ace_ability.cpp",
      "$ace_root/adapter/ohos/entrance/ace_application_info.cpp",
      "$ace_root/adapter/ohos/entrance/ace_container.cpp",
      "$ace_root/adapter/ohos/entrance/ace_data_ability.cpp",
      "$ace_root/adapter/ohos/entrance/ace_form_ability.cpp",
      "$ace_root/adapter/ohos/entrance/ace_new_pipe_judgement.cpp",
      "$ace_root/adapter/ohos/entrance/ace_service_ability.cpp",
      "$ace_root/adapter/ohos/entrance/ace_view_ohos.cpp",
      "$ace_root/adapter/ohos/entrance/capability_registry.cpp",
      "$ace_root/adapter/ohos/entrance/data_ability_helper_standard.cpp",
      "$ace_root/adapter/ohos/entrance/dialog_container.cpp",
      "$ace_root/adapter/ohos/entrance/file_asset_provider.cpp",
      "$ace_root/adapter/ohos/entrance/file_asset_provider_impl.cpp",
      "$ace_root/adapter/ohos/entrance/form_utils_impl.cpp",
      "$ace_root/adapter/ohos/entrance/hap_asset_provider.cpp",
      "$ace_root/adapter/ohos/entrance/hap_asset_provider_impl.cpp",
      "$ace_root/adapter/ohos/entrance/mmi_event_convertor.cpp",
      "$ace_root/adapter/ohos/entrance/pa_container.cpp",
      "$ace_root/adapter/ohos/entrance/plugin_utils_impl.cpp",
      "$ace_root/adapter/ohos/entrance/subwindow/subwindow_ohos.cpp",

      # innerkits impl
      "$ace_root/adapter/ohos/entrance/ui_content_impl.cpp",
      "$ace_root/adapter/ohos/entrance/ui_event_impl.cpp",
      "$ace_root/adapter/ohos/entrance/utils.cpp",
      "$flutter_root/engine/flutter/shell/common/thread_host.cc",

      # drag window
      "$ace_root/adapter/ohos/entrance/window/drag_window_ohos.cpp",

      # ArkTs Card
      "$ace_root/adapter/ohos/entrance/form_module_preloader.cpp",
    ]

    if (target_cpu == "arm64") {
      defines += [ "_ARM64_" ]
    }
    if (current_os == "ohos" && current_cpu == "x86_64") {
      defines += [ "SIMULATOR_64" ]
    }

    configs = [
      "$ace_root:ace_config",
      "$ace_flutter_engine_root:flutter_config",
      "$ace_flutter_engine_root/icu:icu_config_$platform",
    ]

    if (defined(config.enable_rosen_backend) && config.enable_rosen_backend) {
      configs += [
        "//foundation/graphic/graphic_2d/rosen/modules/render_service_base:export_config",
        "//foundation/graphic/graphic_2d/rosen/modules/render_service_client:render_service_client_config",
      ]
    }

    include_dirs = [
      "$ability_runtime_inner_api_path/runtime/include/",
      "$ability_runtime_napi_path/featureAbility",
      "//commonlibrary/c_utils/base/include",
      "//foundation/arkui/napi/interfaces/kits",
      "//foundation/arkui/napi/native_engine",
      "//foundation/window/window_manager/interfaces/innerkits",
      "//foundation/graphic/graphic_2d/rosen/modules/2d_engine",
      "//foundation/graphic/graphic_2d/rosen/modules/2d_graphics/src/drawing/engine_adapter",
      "//third_party/node/src",
    ]

    deps = [
      "$ace_flutter_engine_root:flutter_engine_common_$platform",
      "$skia_root_new:skia_$platform",
    ]

    deps += [
      "$ace_root/adapter/ohos/capability:ace_capability_ohos",
      "$ace_root/interfaces/inner_api/ace:ace_uicontent",
      "//third_party/zlib:shared_libz",
    ]

    external_deps = [
      "ability_base:base",
      "ability_base:configuration",
      "ability_base:extractortool",
      "ability_base:view_data",
      "ability_base:want",
      "ability_runtime:ability_context_native",
      "ability_runtime:ability_manager",
      "ability_runtime:abilitykit_native",
      "ability_runtime:auto_fill_manager",
      "ability_runtime:data_ability_helper",
      "common_event_service:cesfwk_innerkits",
      "data_share:datashare_consumer",
      "ets_runtime:libark_jsruntime",
      "eventhandler:libeventhandler",
      "form_fwk:fmskit_native",
      "form_fwk:form_manager",
      "graphic_2d:2d_graphics",
      "graphic_2d:librender_service_client",
      "i18n:intl_util",
      "input:libmmi-client",
      "ipc:ipc_core",
      "ipc:ipc_napi_common",
      "napi:ace_napi",
      "preferences:native_preferences",
      "relational_store:native_dataability",
      "relational_store:native_rdb",
      "resource_management:global_resmgr",
      "window_manager:libdm",
      "window_manager:libwm",
      "window_manager:libwsutils",
    ]

    if (defined(config.media_library_exists) && config.media_library_exists) {
      include_dirs += [ "//foundation/multimedia/media_library/interfaces/inner_api/media_library_helper/include" ]
      external_deps += [ "media_library:media_library_manager" ]
    }

    if (enable_graphic_text_gine) {
      external_deps += [ "graphic_2d:rosen_text" ]
    }
  }
}

foreach(item, ace_platforms) {
  platform = item.name
  if (platform == "ohos" || platform == "ohos_ng") {
    ace_ohos_standard_source_set("ace_ohos_standard_entrance_${platform}") {
      platform = item.name
      defines = []
      cflags_cc = []
      config = {
      }

      if (defined(item.config)) {
        config = item.config
      }

      if (defined(config.defines)) {
        defines = config.defines
      }

      if (defined(config.cflags_cc)) {
        cflags_cc = config.cflags_cc
      }
    }
  }
}
